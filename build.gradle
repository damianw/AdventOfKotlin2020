apply plugin: 'java'
apply plugin: 'kotlin'

buildscript {

    ext.kotlin_version = '1.3.61'

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }

}

sourceSets {
    main {
        kotlin {
            srcDir 'src/main/kotlin'
        }
        resources {
            srcDir 'src/main/resources'
        }
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += "-Xuse-experimental=kotlin.Experimental"
    }
}

jar {
    manifest {
        attributes 'Main-Class': 'wtf.log.xmas2020.MainKt'
    }
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

def binaryFile = new File(buildDir, "libs/$name-binary.jar")
task binaryJar {
    doLast {
        def jarFile = jar.archivePath

        binaryFile.getParentFile().mkdirs()
        binaryFile << "#!/bin/sh\n\nexec java -jar \$0 \"\$@\"\n\n"
        jarFile.withInputStream { binaryFile.append it }

        binaryFile.setExecutable true, false
    }
}
binaryJar.dependsOn(jar)

task copyBinary(type: Copy, dependsOn: 'binaryJar') {
    from(binaryFile)
    into buildDir
    rename(".*", project.name)
}
tasks.getByName('assemble').dependsOn('copyBinary')

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.2"
    compile "org.jetbrains.kotlinx:kotlinx-coroutines-rx2:1.3.2"
    compile "org.apache.commons:commons-math3:3.6.1"
    compile "com.squareup.okio:okio:2.4.1"
    compile "com.beust:jcommander:1.78"
    compile "io.reactivex.rxjava2:rxjava:2.2.15"
    compile "com.google.guava:guava:28.1-jre"
}
